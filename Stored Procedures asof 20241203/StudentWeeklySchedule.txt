USE [SchoolTrial01]
GO
/****** Object:  StoredProcedure [dbo].[StudentWeeklySchedule]    Script Date: 12/3/2024 10:37:41 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:		Tom Romano
-- Create date: 11/29/2024
-- Description:	Weekly Schedule for Student
-- =============================================
ALTER PROCEDURE [dbo].[StudentWeeklySchedule] 
@StudentID Int
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;

SELECT SCS.StudentClassSessionID, CS.ClassSessionID, ST.StudentID, CO.CourseID, IT.InstructorID, 
	CASE 
		WHEN CS.WeekDay = 'Monday' THEN 1
		WHEN CS.WeekDay = 'Tuesday' THEN 2
		WHEN CS.WeekDay = 'Wednesday' THEN 3
		WHEN CS.WeekDay = 'Thursday' THEN 4
		WHEN CS.WeekDay = 'Friday' THEN 5
		WHEN CS.WeekDay = 'Saturday' THEN 6
		ELSE 7
	End As DayNumber,  CS.WeekDay,
	CO.Title As CourseName, CS.StartTime, CS.EndTime,
	(TRIM(STR(CR.BuildingNumber)) + '/' + TRIM(STR(CR.RoomNumber))) As BuildingRoom,
	(IT.FirstName + ' ' + IT.LastName) As InstructorName
FROM ClassSessions CS
INNER JOIN StudentClassSessions SCS ON CS.ClassSessionID = SCS.ClassSessionID
INNER JOIN Courses CO ON CS.CourseID = CO.CourseID
INNER JOIN Instructors IT ON CS.InstructorID = IT.InstructorID
INNER JOIN ClassRooms CR ON CS.ClassRoomID = CR.ClassRoomID
INNER JOIN Students ST ON SCS.StudentID = ST.StudentID
WHERE ST.StudentID = @StudentID
ORDER BY DayNumber, CS.StartTime
END
